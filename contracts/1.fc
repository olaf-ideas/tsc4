{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}

builder begin_cell() asm "NEWC";
cell end_cell(builder b) asm "ENDC";
int is_null(tuple t) asm "ISNULL";
forall X -> X null() asm "PUSHNULL";
int cell_hash(cell c) asm "HASHCU";
int slice_refs(slice s) asm "SREFS";
(slice, cell) load_ref(slice s) asm( -> 1 0) "LDREF";
slice begin_parse(cell c) asm "CTOS";

(tuple, ()) push_back(tuple tail, cell head) asm "CONS";
(tuple, cell) pop_back(tuple list) asm "UNCONS";

(cell) find_branch_by_hash(int hash, cell tree) inline method_id {

    if (hash == cell_hash(tree)) {
        return tree;
    }

    tuple queue = null();
    queue~push_back(tree);

    do {
        cell node = queue~pop_back();

        slice adj = begin_parse(node);

        repeat (slice_refs(adj)) {
            cell neigh = adj~load_ref();

            if (hash == cell_hash(neigh)) {
                return neigh;
            }

            queue~push_back(neigh);
        }
    } until (is_null(queue));

    return begin_cell().end_cell();
}