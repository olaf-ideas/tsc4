{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

int is_null(tuple t) asm "ISNULL";
int ilen(int x) asm "UBITSIZE";
int is_prefix(slice pattern, slice text) asm "SDPFX";

(tuple, ()) push_back(tuple tail, builder head) asm "CONS";
(tuple, builder) pop_back(tuple list) asm "UNCONS";

(int, slice) load_zeros(slice s) asm "LDZEROES";
builder store_one(builder b) asm "STONE";
builder store_zeros(builder b, int cnt) asm "STZEROES";

builder store_ref(builder b, cell c) asm(c b) "STREF";
(slice, cell) load_ref(slice s) asm( -> 1 0) "LDREF";
int slice_refs(slice s) asm "SREFS";
int min(int x, int y) asm "MIN";
builder store_slice(builder b, slice s) asm "STSLICER";
(slice, ()) ~skip_bits(slice s, int len) asm "SDSKIPFIRST";
forall X -> X null() asm "PUSHNULL";
int slice_bits(slice s) asm "SBITS";
builder begin_cell() asm "NEWC";
slice begin_parse(cell c) asm "CTOS";
cell end_cell(builder b) asm "ENDC";
int builder_bits(builder b) asm "BBITS";

() recv_internal() {
}

(tuple, builder) store_request(tuple list, builder b, int bits) inline {
  if (b.builder_bits() + bits > 1023) {
    list~push_back(b);
    b = begin_cell();
  }

  return (list, b);
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  
  int F = ilen(flag);
  int V = ilen(value);
  
  slice df = begin_cell().store_uint(flag,  F).end_cell().begin_parse();
  slice dv = begin_cell().store_uint(value, V).end_cell().begin_parse();
  
  slice ds = linked_list.begin_parse();
  slice w = ds~load_bits(ds.slice_bits());

  tuple l = null();
  builder b = begin_cell();

  do {

    while (w.slice_bits() >= F) {
      (int zeros, w) = w.load_zeros();

      if (zeros > 0) {
        (l, b) = store_request(l, b, zeros);
        b = b.store_zeros(zeros);
      }
      else {
        if (is_prefix(df, w)) {
          w~skip_bits(F);

          (l, b) = store_request(l, b, V);
          b = b.store_slice(dv);
        }
        else {
          w~skip_bits(1);
        
          (l, b) = store_request(l, b, 1);
          b = b.store_uint(1, 1);
        }
      }
    }

    int ok = false;

    do {
      if (ds.slice_bits() > 0) {
        w = begin_cell().store_slice(w).store_slice(ds~load_bits(min(1023 - w.slice_bits(), ds.slice_bits()))).end_cell().begin_parse();
      }
      else {
        if (ds.slice_refs() > 0) {
          slice next_ds = (ds~load_ref()).begin_parse();
          w = begin_cell().store_slice(w).store_slice(next_ds~load_bits(min(next_ds.slice_bits(), 1023 - w.slice_bits()))).end_cell().begin_parse();
          ds = next_ds;
        }
        else {
          ok = true;
        }
      }

      if (w.slice_bits() >= F) {
        ok = true;
      }

    } until (ok);

  } until (w.slice_bits() < F);

  (l, b) = store_request(l, b, w.slice_bits());

  cell result = b.store_slice(w).end_cell();

  while (~ is_null(l)) {
    builder last = l~pop_back();
    result = last.store_ref(result).end_cell();
  }

  return result;
}
