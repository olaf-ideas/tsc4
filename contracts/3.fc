{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

forall X -> (tuple, X) tpop(tuple t) asm "TPOP";
forall X -> (tuple, X) ~tpop(tuple t) asm "TPOP";

int tlen(tuple t) asm "TLEN";

() recv_internal() {
}

int log2(int n) {
  int result = 0;

  while (n > 0) {
    result += 1;
    n >>= 1;
  }

  return result;
}

tuple to_tuple(int mask) {
  tuple result = empty_tuple();

  ;; while (mask > 0) {
  ;;   result~tpush(mask % 2);
  ;;   mask >>= 1;
  ;; }

  int len = log2(mask);

  while (len > 0) {
    len -= 1;
    result~tpush((mask >> len) % 2);
  }

  return result;
}

(tuple, int) pop_front(tuple t) {
  int value = first(t);

  tuple result = empty_tuple();
  
  int len = tlen(t);

  int i = 1;
  while (i < len) {
    int x = t.at(i);
    result~tpush(x);
    i += 1;
  }

  return (result, value);
}

int tuple_equal(tuple a, tuple b) {
  int lenA = tlen(a);
  int lenB = tlen(b);

  if (lenA != lenB) {
    return false;
  }

  repeat (lenA) {
    (a, int bita) = tpop(a);
    (b, int bitb) = tpop(b);

    if (bita != bitb) {
      return false;
    }
  }

  return true;
}

cell solve(tuple flag, tuple value, int timer, tuple window, slice data) {
  builder result = begin_cell();
  
  while (~ slice_empty?(data)) {

    if (tlen(window) >= tlen(flag)) {
      if (result.builder_bits() == 1023) {
        cell next_chunk = solve(flag, value, timer, window, data);

        result = result.store_ref(next_chunk);

        return result.end_cell();
      }

      (window, int save) = pop_front(window);

      if (timer > 0) {
        timer -= 1;
      }

      result = store_uint(result, save, 1);
    }

    if (slice_data_empty?(data)) {
      ifnot (slice_refs_empty?(data)) {
        cell xd = data.preload_ref();
        slice nxt = xd.begin_parse();

        data = nxt;
      }
    }

    if (tlen(window) < tlen(flag)) {
      ifnot (slice_empty?(data)) {
        (data, int bit) = data.load_uint(1);
        window~tpush(bit);
      }
    }

    if (timer == 0) {
      if (tuple_equal(window, flag) == true) {
        window = value;
        timer = tlen(value);
      }
    }
  }

  while (tlen(window) > 0) {
    if (result.builder_bits() == 1023) {
      cell next_chunk = solve(flag, value, timer, window, data);

      result = result.store_ref(next_chunk);

      return result.end_cell();
    }

    (window, int save) = pop_front(window);
    result = store_uint(result, save, 1);
  }

  return result.end_cell();
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) inline method_id {
  slice data = linked_list.begin_parse();

  tuple flagT = to_tuple(flag);
  tuple valueT = to_tuple(value);
  tuple window = empty_tuple();

  return solve(flagT, valueT, 0, window, data);
}