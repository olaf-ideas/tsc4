{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}

() recv_internal() {
}

forall X -> X first(tuple t) asm "FIRST";
int tlen(tuple t) asm "TLEN";

;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id {

  int n = tlen(matrixA);
  int m = tlen(matrixB);
  int p = tlen(first(matrixB));

  tuple result = empty_tuple();

  int i = 0;
  while (i < n) {
    tuple row = empty_tuple();

    int j = 0;
    while (j < p) {
      int value = 0;

      int k = 0;
      while (k < m) {
        value += matrixA.at(i).at(k) * matrixB.at(k).at(j);

        k += 1;
      }

      row~tpush(value);
      j += 1;
    }

    result~tpush(row);
    i += 1;
  }

  return result;
}